/*
 * Thrifty
 *
 * Copyright (c) Benjamin Bader
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */
plugins {
    id 'thrifty-kotlin-module'
    id 'thrifty-publish-module'
    id 'antlr'
}

description = 'A simple Thrift IDL parser and validator'

generateGrammarSource {
    arguments += ["-package", "com.bendb.thrifty.schema.antlr"]
}

dependencies {
    antlr libs.antlr
    implementation libs.antlr

    api libs.guava
    api libs.bundles.kotlin

    implementation libs.okio

    testImplementation libs.bundles.testing
}

// Antlr doesn't interact nicely with tasks from Kotlin or Dokka (or non-java tasks in general),
// so we need to manually specify the dependency between the two antlr tasks and anything that
// might consume their output.
def mainTaskNames = [
        'compileKotlin',
        'javaSourcesJar',
        'kotlinSourcesJar',
        'sourcesJar',
        'spotlessJava',
        'dokkaHtml',
        'dokkaGeneratePublicationHtml',
].toSet()

def testTaskNames = [
        'compileTestKotlin'
].toSet()

tasks.matching { mainTaskNames.contains(it.name) }.configureEach {
    dependsOn 'generateGrammarSource'
}

tasks.matching { testTaskNames.contains(it.name) }.configureEach {
    dependsOn 'generateTestGrammarSource'
}
